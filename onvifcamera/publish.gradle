apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = "com.seanproctor"
version = "1.3.0"

File propsFile = project.rootProject.file('local.properties')

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

if (propsFile.exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream("local.properties"))
    properties.each { name, value ->
        ext[name] = value
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId "com.seanproctor"
            artifactId "onvifcamera"
            version "1.3.0"

            // Two artifacts, the `aar` (or `jar`) and the sources
            if (project.plugins.findPlugin("com.android.library")) {
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            } else {
                artifact("$buildDir/libs/${project.getName()}-${version}.jar")
            }
            artifact androidSourcesJar

            // Mostly self-explanatory metadata
            pom {
                name = 'onvifcamera'
                description = 'An android library to interact with ONVIF cameras.'
                url = 'https://github.com/sproctor/ONVIFCameraAndroid'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/sproctor/ONVIFCameraAndroid/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sproctor'
                        name = 'Sean Proctor'
                        email = 'sproctor@gmail.com'
                    }
                }
                // Version control info - if you're using GitHub, follow the format as seen here
                scm {
                    connection = 'scm:git:github.com/github.com/sproctor/ONVIFCameraAndroid.git'
                    developerConnection = 'scm:git:ssh://github.com/sproctor/ONVIFCameraAndroid.git'
                    url = 'https://github.com/sproctor/ONVIFCameraAndroid/tree/main'
                }
                // A slightly hacky fix so that your POM will include any transitive dependencies
                // that your library builds upon
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}